// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
      
      Or(a=instruction[15], b=false, out=isC);
      Not(in=instruction[15],        out=isA);    
      
      And(a=isC, b=instruction[5], out=CWriteA);               
      Or(a=isA, b=CWriteA, out=loadA);                         
      Mux16(a=instruction, b=outALU, sel=CWriteA, out=ARegin);
      ARegister(in=ARegin, load=loadA, out=Aregout, out[0..14]=addressM); 
 
      And(a=isC, b=instruction[4], out=loadD);                   
      DRegister(in=outALU, load=loadD, out=DRegout);

      Mux16(a=Aregout, b=inM, sel=instruction[12], out=outAorM);
      
      ALU(
          x=DRegout,
          y=outAorM,
          zx=instruction[11],   
          nx=instruction[10],  
          zy=instruction[9],  
          ny=instruction[8],  
          f =instruction[7],
          no=instruction[6],
          out=outALU,
          out=outM,
          zr=isZero,
          ng=isNeg
        );
          
      Not(in=isNeg, out=isNonNeg);                    
      Not(in=isZero, out=isNonZero);
      And(a=isNonNeg, b=isNonZero, out=isPositive);    //if positive
      
      And(a=isC, b=instruction[3], out=writeM); 
      
      And(a=isPositive, b=instruction[0], out=JGT);    //if positive and GT 0
      And(a=isZero, b=instruction[1], out=JEQ);     //if positive and = 0
      And(a=isNeg, b=instruction[2], out=JLT);      //if positive and LT 0
          
      Or(a=JEQ, b=JLT, out=JLE);
      Or(a=JLE, b=JGT, out=jumpToA);                  
      And(a=isC, b=jumpToA, out=loadPC);
      Not(in=loadPC, out=PCinc);
      PC(in=Aregout, inc=PCinc, load=loadPC, reset=reset, out[0..14]=pc);
}